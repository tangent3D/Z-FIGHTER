; Note: While this macro attempts to set flow control to RTS/CTS, an apparent
; issue in Tera Term 4.106 prevents this from working. Please be sure to
; manually set flow control to RTS/CTS in Tera Term serial port setup and save
; it to TERATERM.INI so that flow control is correctly configured on startup.
; Failure to do so may prevent XMODEM transfers.

; Specify COM port and connection settings (e.g. '/C=1' for port COM1)
connect '/C=1'
setbaud 115200
setflowctrl 1

if result != 2 goto error1

; Display mode of Tera Term (0 = minimize, 1 = restore, -1 = hide)
showtt 1

dispstr 'Macro loaded. Press reset button on Z-Fighter!'#13#10

wait 'console'

; Boot Z-Fighter into CP/M
send #32#120#121
wait 'A>'

; Erase any existing versions of the file we want to send
send 'era 'params[3]#13
wait 'A>'

; Prepare Z-Fighter to receive file via XMODEM
; Desired file name & ext. on Z-Fighter is third argument for TTPMACRO.EXE
send 'xmodem 'params[3]' /r /c'#13

wait 'checksums'

; Send the file from TeraTerm via XMODEM with checksums
xmodemsend params[2] 1
if result=0 goto error2

; Execute the received file on Z-Fighter by calling its base name
; (fourth argument for TTPMACRO.EXE)
wait 'A>'
send params[4]#13

; Close Tera Term
;closett

end

:error1
messagebox 'Connection or link error!'
end

:error2
messagebox 'XMODEM error!'
end